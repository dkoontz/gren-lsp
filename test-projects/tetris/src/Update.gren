module Update exposing ( update )

import Cmd
import Dedris.Motion as Motion exposing ( Motion )
import Dedris.Tetromino as Tmino exposing ( Tetromino )
import Dedris.Tower as Tower
import Model exposing ( Model )
import Msg as Msg exposing ( Msg )
import Random


-- Reagiere auf interne Nachrichten
update : Msg -> Model -> { model : Model , command : Cmd Msg }
update msg mdl =
    if mdl.pause
    then
        case msg of
            Msg.KeyDown _ -> togglePause mdl
            _ -> { model = mdl , command = Cmd.none }
    else
        case msg of
            Msg.ActiveMotion motion -> activeMotion motion mdl
            Msg.MoveDown -> moveDown mdl
            Msg.MoveLeft -> moveLeft mdl
            Msg.MoveRightly -> moveRight mdl
            Msg.NewTmino tmino -> newTmino tmino mdl
            Msg.Reload -> reload mdl
            Msg.RotateLeft -> rotateLeft mdl
            Msg.RotateRight -> rotateRight mdl
            Msg.Tick -> moveDown mdl
            Msg.Viewport vp -> viewport vp mdl
            -- Pfeiltasten
            Msg.KeyDown { key = Just "ArrowUp" , shiftKey = False } -> rotateRight mdl
            Msg.KeyDown { key = Just "ArrowUp" , shiftKey = True } -> rotateLeft mdl
            Msg.KeyDown { key = Just "ArrowLeft" , shiftKey = False } -> moveLeft mdl
            Msg.KeyDown { key = Just "ArrowRight" , shiftKey = False } -> moveRight mdl
            Msg.KeyDown { key = Just "ArrowDown" , shiftKey = False } -> moveDown mdl
            -- hjkl
            Msg.KeyDown { key = Just "k" , shiftKey = False } -> rotateRight mdl
            Msg.KeyDown { key = Just "K" , shiftKey = True } -> rotateLeft mdl
            Msg.KeyDown { key = Just "h" , shiftKey = False } -> moveLeft mdl
            Msg.KeyDown { key = Just "l" , shiftKey = False } -> moveRight mdl
            Msg.KeyDown { key = Just "j" , shiftKey = False } -> moveDown mdl
            -- Leertaste
            Msg.KeyDown { key = Just " " , shiftKey = False } -> togglePause mdl
            --
            _ -> { model = mdl , command = Cmd.none }


-- Bewege Tetromino im Spielturm um eine Position nach unten
-- oder lege ihn ab und hole den nächsten
moveDown : Model -> { model : Model , command : Cmd Msg }
moveDown mdl =
    if canMoveDown mdl
    then move { row = mdl.anchor.row + 1 , col = mdl.anchor.col } mdl.tmino mdl
    else { model = tminoDone mdl , command = Random.generate Msg.NewTmino Tmino.random }


-- Bewege Tetromino im Spielturm nach links
moveLeft : Model -> { model : Model, command : Cmd Msg }
moveLeft mdl =
    move { row = mdl.anchor.row , col = mdl.anchor.col - 1 } mdl.tmino mdl


-- Bewege Tetromino im Spielturm nach rechts
moveRight : Model -> { model : Model, command : Cmd Msg }
moveRight mdl =
    move { row = mdl.anchor.row , col = mdl.anchor.col + 1 } mdl.tmino mdl


-- Bewege Tetromino wenn möglich im Spielturm an eine andere Position
move : { row : Int , col : Int } -> Tetromino -> Model -> { model : Model, command : Cmd Msg }
move anchor tmino mdl =
    if Tower.fits anchor mdl.tower tmino && not mdl.gameOver
    then { model = { mdl | anchor = anchor , tmino = tmino } , command = Cmd.none }
    else { model = mdl , command = Cmd.none }


-- Rotiere Tetromino im Spielturm im Uhrzeigersinn
rotateRight : Model -> { model : Model, command : Cmd Msg }
rotateRight mdl =
    let
        tmino = Tmino.rotate mdl.tmino
        anchor1 = { row = mdl.anchor.row , col = mdl.anchor.col + 1 }
        anchor2 = { row = mdl.anchor.row , col = mdl.anchor.col - 1 }
        anchor3 = { row = mdl.anchor.row , col = mdl.anchor.col + 2 }
        anchor4 = { row = mdl.anchor.row , col = mdl.anchor.col - 2 }
    in
        if Tower.fits mdl.anchor mdl.tower tmino then move mdl.anchor tmino mdl
        else if Tower.fits anchor1 mdl.tower tmino then move anchor1 tmino mdl
        else if Tower.fits anchor2 mdl.tower tmino then move anchor2 tmino mdl
        else if Tower.fits anchor3 mdl.tower tmino then move anchor3 tmino mdl
        else move anchor4 tmino mdl


-- Rotiere Tetromino im Spielturm gegen den Uhrzeigersinn
rotateLeft : Model -> { model : Model, command : Cmd Msg }
rotateLeft mdl =
    case mdl.tmino.type_ of
        Tmino.I -> rotateRight mdl
        Tmino.J -> rotateRight { mdl | tmino = Tmino.rotate ( Tmino.rotate mdl.tmino ) }
        Tmino.L -> rotateRight { mdl | tmino = Tmino.rotate ( Tmino.rotate mdl.tmino ) }
        Tmino.O -> rotateRight mdl
        Tmino.S -> rotateRight mdl
        Tmino.T -> rotateRight { mdl | tmino = Tmino.rotate ( Tmino.rotate mdl.tmino ) }
        Tmino.Z -> rotateRight mdl


-- Hole neuen Tetromino ins Spiel oder beende das laufende Spiel
newTmino : Tmino.Type -> Model -> { model : Model, command : Cmd Msg }
newTmino type_ mdl =
    if Tower.fits initialAnchor mdl.tower mdl.next
    then
        { model =
            { mdl
                | tmino = mdl.next
                , next = Tmino.new type_
                , anchor = initialAnchor
                , tickerMillis = mdl.tickerMillis * 0.999
            }
        , command = Cmd.none
        }
    else
        { model = { mdl | gameOver = True }
        , command = Cmd.none
        }


-- Beginne ein neues Spiel
reload : Model -> { model : Model, command : Cmd Msg }
reload mdl =
    { model =
        { tower = Tower.new
        , tmino = mdl.tmino
        , next = mdl.next
        , anchor = initialAnchor
        , score = 0
        , gameOver = False
        , tickerMillis = 1000
        , pause = False
        , viewport = { height = mdl.viewport.height , width = mdl.viewport.width }
        , activeMotion = Motion.None
        }
    , command = Cmd.generateNewTetromino
    }


-- Wechsle zwischen Pause und laufendem Spiel
togglePause : Model -> { model : Model, command : Cmd Msg }
togglePause mdl =
    { model = { mdl | pause = not mdl.pause }
    , command = Cmd.none
    }


-- Speichere die Höhe und Breite der Anzeige
viewport : { height : Int , width : Int } -> Model -> { model : Model, command : Cmd Msg }
viewport vp mdl =
    { model = { mdl | viewport = vp }
    , command = Cmd.none
    }


-- Lege fest, welche Bewegung gerade aktiv ist
activeMotion : Motion -> Model -> { model : Model, command : Cmd Msg }
activeMotion motion mdl =
    case motion of
        Motion.None -> { model = { mdl | activeMotion = Motion.None } , command = Cmd.none }
        Motion.MoveLeft -> moveLeft { mdl | activeMotion = Motion.MoveLeft }
        Motion.MoveRightly -> moveRight { mdl | activeMotion = Motion.MoveRightly }
        Motion.MoveDown -> moveDown { mdl | activeMotion = Motion.MoveDown }
        Motion.RotateLeft -> rotateLeft { mdl | activeMotion = Motion.RotateLeft }
        Motion.RotateRight -> rotateRight { mdl | activeMotion = Motion.RotateRight }



-- Hilfsfunktionen


-- Prüfe, ob der Tetromino sich im Spielfeld um eine Position nach unten
-- bewegen kann
canMoveDown : Model -> Bool
canMoveDown mdl =
    Tower.fits
        { row = mdl.anchor.row + 1
        , col = mdl.anchor.col
        }
        mdl.tower
        mdl.tmino


-- Lege die Blöcke des aktiven Tetromino auf den Blockstapel und hole einen
-- neuen Tetromino ins Spiel
tminoDone : Model -> Model
tminoDone mdl =
    let
        { tower , eliminated } = mdl.tower |> Tower.set mdl.anchor mdl.tmino |> Tower.shrink
        score = eliminated * ( if eliminated < 4 then 10 else 20 )
    in
        { mdl
            | tower = tower
            , score = mdl.score + score
        }


-- Die Startposition für neu ins Spiel geholte Tetrominos
initialAnchor : { row : Int , col : Int }
initialAnchor = { row = 0 , col = 3 }
